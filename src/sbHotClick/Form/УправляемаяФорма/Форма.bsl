
&НаКлиенте
Перем НужноЗакрыть;

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ВосстановитьДанныеТабличныхЧастейНаСервере();
КонецПроцедуры

&НаСервере
Процедура ВосстановитьДанныеТабличныхЧастейНаСервере()
	ОбъектНаСервере = РеквизитФормыВЗначение("Объект");
	
	ИменаТабличныхЧастей = Новый Массив;
	
	ИменаТабличныхЧастей.Добавить("Состав");
	
	Для Каждого Имя Из ИменаТабличныхЧастей Цикл
		ЗагруженныеНастройки = ХранилищеОбщихНастроек.Загрузить(ЭтаФорма, Имя);
		Если ЗначениеЗаполнено(ЗагруженныеНастройки) Тогда
			ОбъектНаСервере[Имя].Загрузить(ЗагруженныеНастройки);
		КонецЕсли;
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(ОбъектНаСервере, "Объект");
КонецПроцедуры

&НаСервере
Процедура СохранитьДанныеТабличныхЧастейНаСервере()
	ИменаТабличныхЧастей = Новый Массив;
	
	ИменаТабличныхЧастей.Добавить("Состав");
	
	Для Каждого Имя Из ИменаТабличныхЧастей Цикл
		ХранилищеОбщихНастроек.Сохранить(ЭтаФорма, Имя, Объект[Имя].Выгрузить()); 
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура СоставПутьНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.Состав.ТекущиеДанные;
	
	ВыборФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	
	ВыборФайла.Фильтр = "Внешняя обработка (*.epf)|*.epf|Внешний отчет (*.erf)|*.erf";
	
	ВыборФайла.Показать(Новый ОписаниеОповещения("СоставПутьНачалоВыбораЗавершение", ЭтотОбъект, ТекущиеДанные));
КонецПроцедуры

&НаКлиенте
Процедура СоставПутьНачалоВыбораЗавершение(ВыбранныеФайлы, ТекущиеДанные) Экспорт
	Если Ложь
		Или ВыбранныеФайлы = Неопределено
		Или ВыбранныеФайлы.Количество() = 0
		Тогда
		
		Возврат;
	КонецЕсли;
	
	Файл = Новый Файл(ВыбранныеФайлы[0]);
	
	ТекущиеДанные.Путь = Файл.ПолноеИмя;
	ТекущиеДанные.ЭтоОбработка = Файл.Расширение = ".epf";
	
	ПараметрыЗаполнения = Новый Структура;
	
	ПараметрыЗаполнения.Вставить("НомерСтроки", ТекущиеДанные.НомерСтроки);
	ПараметрыЗаполнения.Вставить("ЭтоОбработка", ТекущиеДанные.ЭтоОбработка);
	
	ЗаполнитьОставшиесяРеквизиты(Файл.ПолноеИмя, ПараметрыЗаполнения);
	
	СохранитьДанныеТабличныхЧастейНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьОставшиесяРеквизиты(ПолноеИмя, ПараметрыЗаполнения) 
	Адрес = "";

	НачатьПомещениеФайла(Новый ОписаниеОповещения("ЗаполнитьОставшиесяРеквизитыЗавершение", ЭтотОбъект, ПараметрыЗаполнения), Адрес, ПолноеИмя, Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьОставшиесяРеквизитыЗавершение(Результат, Адрес, ВыбранноеИмяФайла, ПараметрыЗаполнения) Экспорт
	Если Результат Тогда
		ДвоичныеДанные = ПолучитьИзВременногоХранилища(Адрес);
		ИмяВременногоФайла = ПолучитьИмяВременногоФайла();
		ДвоичныеДанные.Записать(ИмяВременногоФайла);
		
		ЗаполнитьОставшиесяРеквизитыНаСервере(ИмяВременногоФайла, ПараметрыЗаполнения);
		
		Файл = Новый Файл(ИмяВременногоФайла);

		Файл.НачатьПроверкуСуществования(Новый ОписаниеОповещения("НачатьПроверкуСуществованияЗавершение", ЭтотОбъект, ИмяВременногоФайла));
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НачатьПроверкуСуществованияЗавершение(Существует, ИмяВременногоФайла) Экспорт
	Если Существует Тогда
		НачатьУдалениеФайлов(Новый ОписаниеОповещения("НачатьУдалениеФайловЗавершение", ЭтотОбъект), ИмяВременногоФайла);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НачатьУдалениеФайловЗавершение(РезультатВызова) Экспорт

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОставшиесяРеквизитыНаСервере(ИмяВременногоФайла, ПараметрыЗаполнения)
	ОбъектНаСервере = РеквизитФормыВЗначение("Объект");
	
	ИскомаяСтрока = ОбъектНаСервере.Состав.Найти(ПараметрыЗаполнения.НомерСтроки, "НомерСтроки");
	Если ИскомаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ВнешняяОбработка = Неопределено;
	Если ПараметрыЗаполнения.ЭтоОбработка Тогда
		ВнешняяОбработка = ВнешниеОбработки.Создать(ИмяВременногоФайла, Ложь);
		
	Иначе
		ВнешняяОбработка = ВнешниеОтчеты.Создать(ИмяВременногоФайла, Ложь);
		
	КонецЕсли;
	
	МетаданныеВнешнейОбработки = ВнешняяОбработка.Метаданные();
	
	ИскомаяСтрока.Имя = МетаданныеВнешнейОбработки.Имя;

	Формы = "";
	
	ФормыВнешнейОбработки = МетаданныеВнешнейОбработки.Формы;
	Для Каждого Форма Из ФормыВнешнейОбработки Цикл
		Формы = Формы + Форма.Имя + ";";
	КонецЦикла;
	
	ИскомаяСтрока.Формы = Формы;
	
	ЗначениеВРеквизитФормы(ОбъектНаСервере, "Объект");
КонецПроцедуры

&НаКлиенте
Процедура СоставФормаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	СписокФорм = Новый СписокЗначений;
	
	ТекущиеДанные = Элементы.Состав.ТекущиеДанные;
	                   
	ИменаФорм = РазложитьСтрокуВМассивПодстрок(ТекущиеДанные.Формы, ";", Истина);
	Для Каждого _ИмяФормы Из ИменаФорм Цикл
		СписокФорм.Добавить(_ИмяФормы, _ИмяФормы);
	КонецЦикла;

	ПоказатьВыборИзСписка(Новый ОписаниеОповещения("СоставФормаНачалоВыбораЗавершение", ЭтотОбъект, ТекущиеДанные), СписокФорм, Элементы.СоставФорма);
КонецПроцедуры

&НаКлиенте
Процедура СоставФормаНачалоВыбораЗавершение(ВыбранныйЭлемент, ТекущиеДанные) Экспорт
	Если ВыбранныйЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ТекущиеДанные.Форма = ВыбранныйЭлемент.Значение;
	
	СохранитьДанныеТабличныхЧастейНаСервере();
КонецПроцедуры

&НаКлиенте
Функция РазложитьСтрокуВМассивПодстрок(Знач Строка, Знач Разделитель = ",", Знач ПропускатьПустыеСтроки = Неопределено, СокращатьНепечатаемыеСимволы = Ложь) Экспорт
	Результат = Новый Массив;

	Если ПропускатьПустыеСтроки = Неопределено Тогда
		ПропускатьПустыеСтроки = ?(Разделитель = " ", Истина, Ложь);
		Если ПустаяСтрока(Строка) Тогда 
			Если Разделитель = " " Тогда
				Результат.Добавить("");
			КонецЕсли;
			Возврат Результат;
		КонецЕсли;
	КонецЕсли;

	Позиция = СтрНайти(Строка, Разделитель);
	Пока Позиция > 0 Цикл
		Подстрока = Лев(Строка, Позиция - 1);
		Если Не ПропускатьПустыеСтроки Или Не ПустаяСтрока(Подстрока) Тогда
			Если СокращатьНепечатаемыеСимволы Тогда
				Результат.Добавить(СокрЛП(Подстрока));
			Иначе
				Результат.Добавить(Подстрока);
			КонецЕсли;
		КонецЕсли;
		Строка = Сред(Строка, Позиция + СтрДлина(Разделитель));
		Позиция = СтрНайти(Строка, Разделитель);
	КонецЦикла;
	
	Если Не ПропускатьПустыеСтроки Или Не ПустаяСтрока(Строка) Тогда
		Если СокращатьНепечатаемыеСимволы Тогда
			Результат.Добавить(СокрЛП(Строка));
		Иначе
			Результат.Добавить(Строка);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции 

&НаКлиенте
Процедура СоставВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если Поле.Имя = "СоставИмя" Тогда
		ТекущиеДанные = Элементы.Состав.ТекущиеДанные;

		Адрес = "";

		НачатьПомещениеФайла(Новый ОписаниеОповещения("СоставВыборЗавершение", ЭтотОбъект, ТекущиеДанные), Адрес, ТекущиеДанные.Путь, Ложь); 
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СоставВыборЗавершение(Результат, Адрес, ВыбранноеИмяФайла, ТекущиеДанные) Экспорт
	Если Результат Тогда
		ИмяВнешнейОбработки = ПодключитьВнешнююОбработкуСервер(Адрес, ТекущиеДанные.ЭтоОбработка);

		ФормаОбработки = ПолучитьФорму(?(ТекущиеДанные.ЭтоОбработка, "ВнешняяОбработка.", "ВнешнийОтчет.") + ТекущиеДанные.Имя + ".Форма." + ТекущиеДанные.Форма + "",, ЭтаФорма);
		
		Если ТекущиеДанные.ЭтоОбработка Тогда
			Если ФормаОбработки.Объект.Свойство("КаталогИнструментов") Тогда
				ФормаОбработки.Объект.КаталогИнструментов = Лев(ВыбранноеИмяФайла, СтрНайти(ВыбранноеИмяФайла, "\", НаправлениеПоиска.СКонца));
			КонецЕсли;
			Если ФормаОбработки.Объект.Свойство("ПредустановленноеИмяФайла") Тогда
				ФормаОбработки.Объект.ПредустановленноеИмяФайла = ВыбранноеИмяФайла;
			КонецЕсли;
		КонецЕсли;

		ФормаОбработки.Открыть();
		
		Если Параметры.Свойство("ЭтоПроверкаСценария") Тогда
			Оповестить("ОкноОткрылось", ФормаОбработки.Окно);	
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ПодключитьВнешнююОбработкуСервер(Адрес, ЭтоОбработка)
	Результат = Неопределено;
	
	Если ЭтоОбработка Тогда
		Результат = ВнешниеОбработки.Подключить(Адрес,, Ложь);
		
	Иначе
		Результат = ВнешниеОтчеты.Подключить(Адрес,, Ложь);
		
	КонецЕсли;

	Возврат Результат;
КонецФункции

&НаКлиенте
Процедура СообщитьПараметрЗапуска(Команда)
	СообщитьПараметрЗапускаНаСервере();
КонецПроцедуры

&НаСервере
Процедура СообщитьПараметрЗапускаНаСервере()
	ИспользуемоеИмяФайла = РеквизитФормыВЗначение("Объект").ИспользуемоеИмяФайла;
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "/EXECUTE """ + ИспользуемоеИмяФайла + """";
	Сообщение.Сообщить();	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	Если Не НужноЗакрыть Тогда
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму(Команда)
	НужноЗакрыть = Истина;
	
	ЭтаФорма.Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьВнешнююОбработку(Команда)
		ТекущиеДанные = Элементы.Состав.ТекущиеДанные;

		Адрес = "";

		НачатьПомещениеФайла(Новый ОписаниеОповещения("СоставВыборЗавершение", ЭтотОбъект, ТекущиеДанные), Адрес, ТекущиеДанные.Путь, Ложь); 
КонецПроцедуры

НужноЗакрыть = Ложь;
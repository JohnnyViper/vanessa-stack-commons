
&НаКлиенте
Процедура Настройки(Команда)
	ОткрытьФорму("ВнешняяОбработка.HotClick.Форма.Настройки");
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьИнструмент(ИмяРеквизита)
	ПолноеИмяФайлаИнструмента = Объект[ИмяРеквизита];

	Если ПустаяСтрока(ПолноеИмяФайлаИнструмента) Тогда
		ПоказатьПредупреждение(, "Проверьте в настройках путь к инструменту");
		
		Возврат;
	КонецЕсли;

	АдресХранилища = "";

	НачатьПомещениеФайла(Новый ОписаниеОповещения("НачатьПомещениеФайлаЗавершение", ЭтотОбъект), АдресХранилища, ПолноеИмяФайлаИнструмента, Ложь, Новый УникальныйИдентификатор); 
КонецПроцедуры

&НаКлиенте
Процедура НачатьПомещениеФайлаЗавершение(Результат, АдресХранилища, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
	Если Результат Тогда
		ИмяВнешнейОбработки = ПодключитьВнешнююОбработкуСервер(АдресХранилища);

		Если ИмяВнешнейОбработки = "BDDEditor" Тогда
			ИмяФормыИнструмента = "ФормаУправляемая";
			
		ИначеЕсли ИмяВнешнейОбработки = "VanessaBehavior" Тогда
			ИмяФормыИнструмента = "УправляемаяФорма";
			
		КонецЕсли;

		ФормаОбработки = ПолучитьФорму("ВнешняяОбработка." + ИмяВнешнейОбработки + ".Форма." + ИмяФормыИнструмента + "",, ЭтаФорма);

		ФормаОбработки.Открыть();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ПодключитьВнешнююОбработкуСервер(АдресХранилища)
	Возврат ВнешниеОбработки.Подключить(АдресХранилища,, Ложь);
КонецФункции

&НаКлиенте
Процедура BDDEditor(Команда)
	ОткрытьИнструмент("ПутьКBDDEditor");
КонецПроцедуры

&НаКлиенте
Процедура Behavior(Команда)
	ОткрытьИнструмент("ПутьКBehavior");
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ВосстановитьПутьКИнструментам();
КонецПроцедуры

&НаСервере
Процедура ВосстановитьПутьКИнструментам()
	ОбъектНаСервере = РеквизитФормыВЗначение("Объект");
	
	Для Каждого Реквизит Из ОбъектНаСервере.Метаданные().Реквизиты Цикл
		ЗагруженноеЗначение = ХранилищеОбщихНастроек.Загрузить(ЭтаФорма, Реквизит.Имя);
		Если ЗначениеЗаполнено(ЗагруженноеЗначение) Тогда
			ОбъектНаСервере[Реквизит.Имя] = ЗагруженноеЗначение;
		КонецЕсли;
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(ОбъектНаСервере, "Объект");
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ОбновитьПутиКИнструментам" Тогда
		ВосстановитьПутьКИнструментам();	
	КонецЕсли;
КонецПроцедуры


&НаКлиенте
Процедура Настройки(Команда)
	ОткрытьФорму("ВнешняяОбработка.HotClick.Форма.Настройки");
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьИнструмент(ПолноеИмяФайл, ИмяРеквизитаФормы)
	ПолноеИмяФайлаИнструмента = Объект[ПолноеИмяФайл];
	ИмяФормыИнструмента = Объект[ИмяРеквизитаФормы];

	Если ПустаяСтрока(ПолноеИмяФайлаИнструмента) Тогда
		ПоказатьПредупреждение(, "Проверьте в настройках путь к инструменту");
		
		Возврат;
	КонецЕсли;

	АдресХранилища = "";

	НачатьПомещениеФайла(Новый ОписаниеОповещения("НачатьПомещениеФайлаЗавершение", ЭтотОбъект, ИмяФормыИнструмента), АдресХранилища, ПолноеИмяФайлаИнструмента, Ложь, Новый УникальныйИдентификатор); 
КонецПроцедуры

&НаКлиенте
Процедура НачатьПомещениеФайлаЗавершение(Результат, АдресХранилища, ВыбранноеИмяФайла, ИмяФормыИнструмента) Экспорт
	Если Результат Тогда
		ИмяВнешнейОбработки = ПодключитьВнешнююОбработкуСервер(АдресХранилища);

		ФормаОбработки = ПолучитьФорму("ВнешняяОбработка." + ИмяВнешнейОбработки + ".Форма." + ИмяФормыИнструмента + "",, ЭтаФорма);
		
		Если ФормаОбработки.Объект.Свойство("КаталогИнструментов") Тогда
			ФормаОбработки.Объект.КаталогИнструментов = Лев(ВыбранноеИмяФайла, СтрНайти(ВыбранноеИмяФайла, "\", НаправлениеПоиска.СКонца));
		КонецЕсли;
		
		ФормаОбработки.Открыть();
		
		Если Параметры.Свойство("ЭтоПроверкаСценария") Тогда
			Оповестить("ОкноОткрылось", ФормаОбработки.Окно);	
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ПодключитьВнешнююОбработкуСервер(АдресХранилища)
	Возврат ВнешниеОбработки.Подключить(АдресХранилища,, Ложь);
КонецФункции

&НаКлиенте
Процедура BDDEditor(Команда)
	ОткрытьИнструмент("ПутьКBDDEditor", "ФормаBDDEditor");
КонецПроцедуры

&НаКлиенте
Процедура Behavior(Команда)
	ОткрытьИнструмент("ПутьКBehavior", "ФормаBehavior");
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ВосстановитьПутьКИнструментам();
КонецПроцедуры

&НаСервере
Процедура ВосстановитьПутьКИнструментам()
	ОбъектНаСервере = РеквизитФормыВЗначение("Объект");
	
	Для Каждого Реквизит Из ОбъектНаСервере.Метаданные().Реквизиты Цикл
		ЗагруженноеЗначение = ХранилищеОбщихНастроек.Загрузить(ЭтаФорма, Реквизит.Имя);
		Если ЗначениеЗаполнено(ЗагруженноеЗначение) Тогда
			ОбъектНаСервере[Реквизит.Имя] = ЗагруженноеЗначение;
		КонецЕсли;
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(ОбъектНаСервере, "Объект");
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ОбновитьПутиКИнструментам" Тогда
		ВосстановитьПутьКИнструментам();	
	КонецЕсли;
КонецПроцедуры

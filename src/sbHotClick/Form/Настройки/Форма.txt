
&НаКлиенте
Процедура ОткрыватьПослеСтартаСистемы(Команда)
	ДиалогВыбораФайлаСпискаОбщихИнфобаз = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	
	ДиалогВыбораФайлаСпискаОбщихИнфобаз.МножественныйВыбор = Ложь;
	ДиалогВыбораФайлаСпискаОбщихИнфобаз.Фильтр = "Файл списка общих инфобаз|*.v8i";
	
	ДиалогВыбораФайлаСпискаОбщихИнфобаз.Показать(Новый ОписаниеОповещения("ДиалогВыбораФайлаСпискаОбщихИнфобазЗавершение", ЭтотОбъект));
КонецПроцедуры

&НаКлиенте
Процедура ДиалогВыбораФайлаСпискаОбщихИнфобазЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	Если Не ЗначениеЗаполнено(ВыбранныеФайлы) Тогда
		Возврат;
	КонецЕсли;
	
	ПолноеИмяФайла = ВыбранныеФайлы[0];
	
	ТекДок = Новый ТекстовыйДокумент;
	
	ДополнительныеПараметры = Новый Структура;
	
	ДополнительныеПараметры.Вставить("ПолноеИмяФайла", ПолноеИмяФайла);
	ДополнительныеПараметры.Вставить("ТекДок", ТекДок);
	
	ТекДок.НачатьЧтение(Новый ОписаниеОповещения("НачатьЧтениеЗавершение", ЭтотОбъект, ДополнительныеПараметры), ПолноеИмяФайла, КодировкаТекста.UTF8);
КонецПроцедуры

&НаКлиенте
Процедура НачатьЧтениеЗавершение(ДополнительныеПараметры) Экспорт
	ТекДок = ДополнительныеПараметры.ТекДок;
	
	СтрокаСоединения = СтрокаСоединенияИнформационнойБазы();

	ПолноеИмяОбработки = ПолучитьПолноеИмяОбработки();
	
	СписокОбщихИнфобазОБработан = Ложь;
	
	ВсегоСтрок = ТекДок.КоличествоСтрок();
	Для Счетчик = 1 По ВсегоСтрок Цикл
		ТекущаяСтрока = ТекДок.ПолучитьСтроку(Счетчик);
		
		Если СтрНайти(ТекущаяСтрока, СтрокаСоединения) = 0 Тогда
			Продолжить;
		КонецЕсли;

		Счетчик = Счетчик + 1;
		
		Для ВложеныйСчетчик = Счетчик По ВсегоСтрок Цикл
			ТекущаяСтрока = ТекДок.ПолучитьСтроку(ВложеныйСчетчик);
			
			Если Лев(ТекущаяСтрока, 1) = "[" Тогда
				Счетчик = ВложеныйСчетчик;

				Прервать;
				
			ИначеЕсли СтрНайти(ТекущаяСтрока, "Version") = 0 Тогда
				Продолжить;
				
			КонецЕсли;

			НомерСледующейСтроки = ВложеныйСчетчик + 1;
			НоваяСтрока = "AdditionalParameters=/EXECUTE """ + ПолноеИмяОбработки + """";
			
			СледующаяСтрока = ТекДок.ПолучитьСтроку(НомерСледующейСтроки);
			Если СтрНайти(СледующаяСтрока, "AdditionalParameters") = 0 Тогда
				
				ТекДок.ВставитьСтроку(НомерСледующейСтроки, НоваяСтрока);

			Иначе
				СледующаяСтрока = СтрЗаменить(СледующаяСтрока, "AdditionalParameters=", НоваяСтрока) + СледующаяСтрока;
				
			КонецЕсли;
			
			ТекДок.НачатьЗапись(Новый ОписаниеОповещения("НачатьЗаписьЗавершение", ЭтотОбъект), ДополнительныеПараметры.ПолноеИмяФайла, КодировкаТекста.UTF8);

			СписокОбщихИнфобазОБработан = Истина;
			
			Если СписокОбщихИнфобазОБработан Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если СписокОбщихИнфобазОБработан Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура НачатьЗаписьЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если Результат Тогда
		Текст = "Файл списка общи инфобаз обновлен успешно!";
		
	Иначе
		Текст = "Не удалось обновить файл списка общих инфобаз!";
		
	КонецЕсли;
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = Текст;
	Сообщение.Сообщить();
КонецПроцедуры

&НаСервере
Функция ПолучитьПолноеИмяОбработки() 
	ОбъектНаСервере = РеквизитФормыВЗначение("Объект");
	
	Возврат ОбъектНаСервере.ИспользуемоеИмяФайла;
КонецФункции

&НаКлиенте
Процедура ПутьКStackCommonsНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ДиалогВыбораКаталогаStackCommons = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);

	ДиалогВыбораКаталогаStackCommons.Показать(Новый ОписаниеОповещения("ДиалогВыбораКаталогаStackCommonsЗавершение", ЭтотОбъект));
КонецПроцедуры

&НаКлиенте
Процедура ДиалогВыбораКаталогаStackCommonsЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	Если Не ЗначениеЗаполнено(ВыбранныеФайлы) Тогда
		Возврат;
	КонецЕсли;
	
	ПутьКVanessaStack = ВыбранныеФайлы[0];
	
	СохранитьПутьКVanessaStackВОбщихНастройках(ПутьКVanessaStack);
КонецПроцедуры

&НаСервере
Процедура СохранитьПутьКVanessaStackВОбщихНастройках(ПутьКVanessaStack) 
	ХранилищеОбщихНастроек.Сохранить(ЭтаФорма, "ПутьКVanessaStack", ПутьКVanessaStack);
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ВосстановитьПутьКVanessaStackИзОбщихНастроек();
КонецПроцедуры

&НаСервере
Процедура ВосстановитьПутьКVanessaStackИзОбщихНастроек() 
	ЗагруженноеЗначение = ХранилищеОбщихНастроек.Загрузить(ЭтаФорма, "ПутьКVanessaStack");
	Если ЗначениеЗаполнено(ЗагруженноеЗначение) Тогда
		ПутьКVanessaStack = ЗагруженноеЗначение;
	КонецЕсли;
КонецПроцедуры

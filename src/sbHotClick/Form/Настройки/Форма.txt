
&НаКлиенте
Процедура ОткрыватьПослеСтартаСистемы(Команда)
	ДиалогВыбораФайлаСпискаОбщихИнфобаз = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	
	ДиалогВыбораФайлаСпискаОбщихИнфобаз.МножественныйВыбор = Ложь;
	ДиалогВыбораФайлаСпискаОбщихИнфобаз.Фильтр = "Файл списка общих инфобаз|*.v8i";
	
	ДиалогВыбораФайлаСпискаОбщихИнфобаз.Показать(Новый ОписаниеОповещения("ДиалогВыбораФайлаСпискаОбщихИнфобазЗавершение", ЭтотОбъект));
КонецПроцедуры

&НаКлиенте
Процедура ДиалогВыбораФайлаСпискаОбщихИнфобазЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	Если Не ЗначениеЗаполнено(ВыбранныеФайлы) Тогда
		Возврат;
	КонецЕсли;
	
	ПолноеИмяФайла = ВыбранныеФайлы[0];
	
	ТекДок = Новый ТекстовыйДокумент;
	
	ДополнительныеПараметры = Новый Структура;
	
	ДополнительныеПараметры.Вставить("ПолноеИмяФайла", ПолноеИмяФайла);
	ДополнительныеПараметры.Вставить("ТекДок", ТекДок);
	
	ТекДок.НачатьЧтение(Новый ОписаниеОповещения("НачатьЧтениеЗавершение", ЭтотОбъект, ДополнительныеПараметры), ПолноеИмяФайла, КодировкаТекста.UTF8);
КонецПроцедуры

&НаКлиенте
Процедура НачатьЧтениеЗавершение(ДополнительныеПараметры) Экспорт
	ТекДок = ДополнительныеПараметры.ТекДок;
	
	СтрокаСоединения = СтрокаСоединенияИнформационнойБазы();
	
	ПолноеИмяОбработки = ПолучитьПолноеИмяОбработки();
	
	СписокОбщихИнфобазОБработан = Ложь;
	
	ВсегоСтрок = ТекДок.КоличествоСтрок();
	Для Счетчик = 1 По ВсегоСтрок Цикл
		ТекущаяСтрока = ТекДок.ПолучитьСтроку(Счетчик);
		
		Если СтрНайти(ТекущаяСтрока, СтрокаСоединения) = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Счетчик = Счетчик + 1;
		
		Для ВложеныйСчетчик = Счетчик По ВсегоСтрок Цикл
			ТекущаяСтрока = ТекДок.ПолучитьСтроку(ВложеныйСчетчик);
			
			Если Лев(ТекущаяСтрока, 1) = "[" Тогда
				Счетчик = ВложеныйСчетчик;
				
				Прервать;
				
			ИначеЕсли СтрНайти(ТекущаяСтрока, "Version") = 0 Тогда
				Продолжить;
				
			КонецЕсли;
			
			НомерСледующейСтроки = ВложеныйСчетчик + 1;
			НоваяСтрока = "AdditionalParameters=/EXECUTE """ + ПолноеИмяОбработки + """";
			
			СледующаяСтрока = ТекДок.ПолучитьСтроку(НомерСледующейСтроки);
			Если СтрНайти(СледующаяСтрока, "AdditionalParameters") = 0 Тогда
				
				ТекДок.ВставитьСтроку(НомерСледующейСтроки, НоваяСтрока);
				
			Иначе
				СледующаяСтрока = СтрЗаменить(СледующаяСтрока, "AdditionalParameters=", НоваяСтрока) + СледующаяСтрока;
				
			КонецЕсли;
			
			ТекДок.НачатьЗапись(Новый ОписаниеОповещения("НачатьЗаписьЗавершение", ЭтотОбъект), ДополнительныеПараметры.ПолноеИмяФайла, КодировкаТекста.UTF8);
			
			СписокОбщихИнфобазОБработан = Истина;
			
			Если СписокОбщихИнфобазОБработан Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если СписокОбщихИнфобазОБработан Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура НачатьЗаписьЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если Результат Тогда
		Текст = "Файл списка общи инфобаз обновлен успешно!";
		
	Иначе
		Текст = "Не удалось обновить файл списка общих инфобаз!";
		
	КонецЕсли;
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = Текст;
	Сообщение.Сообщить();
КонецПроцедуры

&НаСервере
Функция ПолучитьПолноеИмяОбработки() 
	ОбъектНаСервере = РеквизитФормыВЗначение("Объект");
	
	Возврат ОбъектНаСервере.ИспользуемоеИмяФайла;
КонецФункции

&НаСервере
Процедура СохранитьРеквизитыВОбщихНастройках()
	ОбъектНаСервере = РеквизитФормыВЗначение("Объект");
	
	МетаданныеРеквизитов = ОбъектНаСервере.Метаданные().Реквизиты;
	Для Каждого МетаданныеРеквизита Из МетаданныеРеквизитов Цикл
		ХранилищеОбщихНастроек.Сохранить(ЭтаФорма, МетаданныеРеквизита.Имя, Объект[МетаданныеРеквизита.Имя]); 
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ВосстановитьПутьКИнструментам();
КонецПроцедуры

&НаСервере
Процедура ВосстановитьПутьКИнструментам()
	ОбъектНаСервере = РеквизитФормыВЗначение("Объект");
	
	МетаданныеРеквизитов = ОбъектНаСервере.Метаданные().Реквизиты;
	Для Каждого МетаданныеРеквизита Из МетаданныеРеквизитов Цикл
		ЗагруженноеЗначение = ХранилищеОбщихНастроек.Загрузить(ЭтаФорма, МетаданныеРеквизита.Имя);
		Если ЗначениеЗаполнено(ЗагруженноеЗначение) Тогда
			ОбъектНаСервере[МетаданныеРеквизита.Имя] = ЗагруженноеЗначение;
		КонецЕсли;
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(ОбъектНаСервере, "Объект");
КонецПроцедуры

&НаКлиенте
Процедура ПутьКBDDEditorНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ДополнительныеПараметры = Новый Структура;
	
	ДополнительныеПараметры.Вставить("Путь", Объект.ПутьКBDDEditor);
	
	ИменаРеквизитов = Новый Структура;
	
	ИменаРеквизитов.Вставить("Путь", "ПутьКBDDEditor");
	ИменаРеквизитов.Вставить("Форма", "ФормаBDDEditor");

	ДополнительныеПараметры.Вставить("ИменаРеквизитов", ИменаРеквизитов);
	
	ДиалогВыбораИнструмента = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	
	ДиалогВыбораИнструмента.Показать(Новый ОписаниеОповещения("ДиалогВыбораИнструментаЗавершение", ЭтотОбъект, ДополнительныеПараметры));
КонецПроцедуры

&НаКлиенте
Процедура ПутьКBehaviorНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;

	ДополнительныеПараметры = Новый Структура;
	
	ДополнительныеПараметры.Вставить("Путь", Объект.ПутьКBehavior);
	
	ИменаРеквизитов = Новый Структура;
	
	ИменаРеквизитов.Вставить("Путь", "ПутьКBehavior");
	ИменаРеквизитов.Вставить("Форма", "ФормаBehavior");

	ДополнительныеПараметры.Вставить("ИменаРеквизитов", ИменаРеквизитов);
	
	ДиалогВыбораИнструмента = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	
	ДиалогВыбораИнструмента.Показать(Новый ОписаниеОповещения("ДиалогВыбораИнструментаЗавершение", ЭтотОбъект, ДополнительныеПараметры));
КонецПроцедуры

&НаКлиенте
Процедура ДиалогВыбораИнструментаЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	Если Не ЗначениеЗаполнено(ВыбранныеФайлы) Тогда
		Возврат;
	КонецЕсли;
	
	Объект[ДополнительныеПараметры.ИменаРеквизитов.Путь] = ВыбранныеФайлы[0];
	
	ЗаполнитьСписокФорм(ДополнительныеПараметры);
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	СохранитьРеквизитыВОбщихНастройках();
	
	Оповестить("ОбновитьПутиКИнструментам");
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСписокФорм(ДополнительныеПараметры)
	Адрес = "";

	НачатьПомещениеФайла(Новый ОписаниеОповещения("НачатьПомещениеФайлаЗавершение", ЭтотОбъект, ДополнительныеПараметры), Адрес, ДополнительныеПараметры.Путь, Ложь);
КонецПроцедуры

&НаКлиенте
Процедура НачатьПомещениеФайлаЗавершение(Результат, Адрес, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
	Если Результат Тогда
		ДвоичныеДанные = ПолучитьИзВременногоХранилища(Адрес);
		ИмяВременногоФайла = ПолучитьИмяВременногоФайла();
		ДвоичныеДанные.Записать(ИмяВременногоФайла);
		
		ЗаполнитьСписокФормНаСервере(ИмяВременногоФайла, ДополнительныеПараметры);
		
		Файл = Новый Файл(ИмяВременногоФайла);
		
		ДополнительныеПараметры.Вставить("Файл", Файл);
		
		Файл.НачатьПроверкуСуществования(Новый ОписаниеОповещения("НачатьПроверкуСуществованияЗавершение", ЭтотОбъект, ДополнительныеПараметры));
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НачатьПроверкуСуществованияЗавершение(Существует, ДополнительныеПараметры) Экспорт
	Если Существует Тогда
		НачатьУдалениеФайлов(Новый ОписаниеОповещения("НачатьУдалениеФайловЗавершение", ЭтотОбъект), ДополнительныеПараметры.Файл.ПолноеИмя);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НачатьУдалениеФайловЗавершение(РезультатВызова) Экспорт

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокФормНаСервере(ИмяВременногоФайла, ДополнительныеПараметры)
	ВнешняяОбработка = ВнешниеОбработки.Создать(ИмяВременногоФайла, Ложь);
	
	Элементы[ДополнительныеПараметры.ИменаРеквизитов.Форма].СписокВыбора.Очистить();
	
	ФормыВнешнейОбработки = ВнешняяОбработка.Метаданные().Формы;
	Для Каждого Форма Из ФормыВнешнейОбработки Цикл
		Элементы[ДополнительныеПараметры.ИменаРеквизитов.Форма].СписокВыбора.Добавить(Форма.Имя, Форма.Имя);
	КонецЦикла;
КонецПроцедуры